repositories {
  mavenLocal()
  mavenCentral()
}

def map_os_to_suffix = [ "win" : "win32.win32", "linux" : "gtk.linux" ]
def map_arch_to_suffix = [ "x86_32" : "x86", "x86_amd_64" : "x86_64" ]

task build

configurations {
  swt_impl
}

dependencies {
  swt_impl "eclipse-juno:org.eclipse.swt:$swt_version"
  swt_impl "eclipse-juno:org.eclipse.jface:$jface_version"
}

project.task([ type: Copy ], "collectSwtDependencies") {
  from project.configurations.swt_impl
  into "${project.projectDir}/build/swt/swt_impl"
}

project.tasks.build.dependsOn "collectSwtDependencies"

for(os in [ "win", "linux" ])
  for(arch in [ "x86_32", "x86_amd_64" ]) {
    def config = "swt_impl_${os}_${arch}"
    project.configurations.add config
    project.dependencies.add config, "eclipse-juno:org.eclipse.swt.${map_os_to_suffix[os]}.${map_arch_to_suffix[arch]}:$swt_version"
    project.task([ type: Copy ], "collectSwtDependencies_${config}") {
      from project.configurations[config]
      into "${project.projectDir}/build/swt/${config}"
    }
    project.tasks.build.dependsOn "collectSwtDependencies_${config}"
  }
