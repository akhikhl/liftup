buildscript {

  repositories {
    mavenLocal()
    mavenCentral()
  }

  apply plugin: 'maven'
  
  dependencies {
    classpath 'org.akhikhl.sweet:sweet-plugin:0.0.3'
  }
}

ext {
  generateSources = true
  generateJavadoc = true
  sweet_version = '0.0.3'
  groovy_version = '2.0.7'
}

allprojects {
  group = 'org.akhikhl.sweet'
  version = sweet_version
}

subprojects {
  apply plugin: 'maven'
  repositories {
    mavenLocal()
    mavenCentral()
  }

  afterEvaluate {
  
    if(plugins.findPlugin('java')) {
      sourceCompatibility = '1.7'
      targetCompatibility = '1.7'
    }
  
    if(generateSources && tasks.findByName('classes')) {
      task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
      }
      artifacts {
        archives sourcesJar
      }
    }

    if(generateJavadoc && tasks.findByName('javadoc')) {
      task javadocJar(type: Jar) {
        dependsOn javadoc
        classifier = 'javadoc'
        from javadoc.destinationDir
        if(tasks.findByName('groovydoc')) {
          dependsOn groovydoc
          from groovydoc.destinationDir
        }
      }
      artifacts {
        archives javadocJar
      }
    }
  }
}

defaultTasks 'install'

